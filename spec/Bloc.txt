Service: Bloc

Types: boolean, enum TypeBloc VIDE, TERRE, MUR, HERO, SORTIE_FERMEE, SORTIE_OUVERTE, ROCHER, DIAMANT

Use: Position

Observators:
	getType : [Bloc] -> TypeBloc
	getPosition : [Bloc] -> Position
	isVide : [Bloc] -> boolean
	isSolide : [Bloc] -> boolean
	isDeplacable : [Bloc] -> boolean
	isTombable : [Bloc] -> boolean
	isSortie : [Bloc] -> boolean
	isSortieFermee : [Bloc] -> boolean
	
Constructors:
	init : TypeBloc * Position -> [Bloc]
	
Operators:
	setType : [Bloc] * TypeBloc -> [Bloc]
	
Observations:
	[invariant]
		isVide(b) ={min} getType(b) = VIDE
		isSolide(b) ={min} getType(b) \in { SORTIE_FERMEE, MUR, ROCHER }
		isDeplacable(b) ={min} getType(b) = ROCHER
		isTombable(b) ={min} getType(b) \in { ROCHER, DIAMANT }
		isSortie(b) ={min} getType(b) \in { SORTIE_FERMEE, SORTIE_OUVERTE }
		isSortieFermee(b) ={min} getType(b) = SORTIE_FERMEE
		
	[init]
		getType(init(tb, pos)) = tb
		getPosition(init(tb, pos)) = pos
		
	[setType]
		getType(setType(b, tb)) = tb
		getPosition(setType(b, tb)) = getPosition(b)